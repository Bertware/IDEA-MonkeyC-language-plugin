import org.jetbrains.intellij.RunIdeaTask

plugins {
  id "org.jetbrains.intellij" version "0.0.43"
}

allprojects {
  apply plugin: 'java'
  sourceCompatibility = javaVersion
  targetCompatibility = javaVersion
  tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }
}

apply plugin: 'org.jetbrains.intellij'

intellij {
  version ideaVersion
  pluginName 'monkey'
  downloadSources Boolean.valueOf(downloadIdeaSources)
  publish {
    username publishUsername
    password publishPassword
    pluginId '8253'
    channel publishChannel
  }
}

group 'io.github.liias'
version pluginVersion

repositories {
  mavenCentral()
}

dependencies {
  compile project(':jps-plugin')
  compile project(':deserializer')
  compile project(':yard-decompiler')
}

sourceSets.main.java.srcDirs += 'src/main/gen'

apply plugin: 'idea'
idea {
  project {
    jdkName = javaVersion
    languageLevel = javaVersion
  }
  module {
    generatedSourceDirs += file('src/main/gen')
  }
}

// Allows to debug compiler (jps-plugin) by running a
// Remote run configuration on the same port after starting build in monkey plugin
// Additionally you have to, in running IDEA, go to Find Action (Ctrl+Shift+A) and enable "Debug Build Process".
afterEvaluate {
  task("debugIdea", type: RunIdeaTask, group: 'intellij', dependsOn: 'prepareSandbox') {
    jvmArgs += ["-Dcompiler.process.debug.port=9128"]
  }
}