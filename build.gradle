import org.jetbrains.intellij.RunIdeaTask

plugins {
  id "org.jetbrains.intellij" version "0.0.43"
}

allprojects {
  apply plugin: 'java'
  sourceCompatibility = 1.8
  targetCompatibility = 1.8
  tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

  sourceSets {
    main {
      java.srcDirs = files('src')
      resources.srcDirs = files('resources')
    }
    test {
      java.srcDirs = files('tests')
      resources.srcDirs = []
    }
  }

  apply plugin: 'org.jetbrains.intellij'

  intellij {
    version '15.0.4'
    //pluginName 'monkey'
    downloadSources true
  }
}

sourceSets.main.java.srcDirs += 'gen'

dependencies {
  compile project(':jps-plugin')
}

apply plugin: 'idea'
idea {
  project {
    jdkName = javaVersion
    languageLevel = javaVersion
  }
  module {
    generatedSourceDirs += file('gen')
  }
}

// Allows to debug compiler (jps-plugin) by running a
// Remote run configuration on the same port after starting build in monkey plugin
afterEvaluate {
  task("debugIdea", type: RunIdeaTask, group: 'intellij', dependsOn: 'prepareSandbox') {
    jvmArgs += ["-Dcompiler.process.debug.port=9128"]
  }
}

group 'io.github.liias'
version '0.2.0-SNAPSHOT'

repositories {
  mavenCentral()
}